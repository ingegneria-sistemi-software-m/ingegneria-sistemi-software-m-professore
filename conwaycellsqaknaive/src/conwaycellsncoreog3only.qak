System conwaycellsnqakaive
mqttBroker "192.168.1.132" : 1883 eventTopic "lifein" 
//mqttBroker "test.mosquitto.org" : 1883 eventTopic "lifeevents" 
       
Event startthegame     : startthegame(X)
Event    stopthecell   : stopthecell(X) 
Event    synch         : synch(X)   
Event    clearCell     : clearCell(X)      
Event curstate         : curstate(NB,STATE)

Event kernel_rawmsg    : kernel_rawmsg(ARG)

Event alarm            : alarm(ARG)  //just to test ...

Dispatch changeCellState   : changeCellState(X,Y)
Dispatch allnbreceived     : allnbreceived(N)

Context ctxcells ip [ host="localhost" port=8360] 
//Context ctxcellonrasp ip [ host="192.168.1.132" port=8075] 
//
//ExternalQActor cell_1_1 context ctxcellonrasp

  
QActor cellbuilder context ctxcells{
import "unibo.basicomm23.mqtt.*"

 State s0 initial{
 	println("$name | STARTS 3 only")
 }
 Goto work 
 
 
 State work{
 	println("$name | WORK")
 	[# createActorDynamically( "cell", "_0_0", false) 
 	   createActorDynamically( "cell", "_0_1", false)
 	   createActorDynamically( "cell", "_1_0", false)	
 	   //createActorDynamically( "cell", "_1_1", false)	
 	#]
 }
} 

 
/*
 * ------------------------------------------
 * cell
 * ------------------------------------------
 */
QActor cell context ctxcells dynamicOnly{
import "main.java.*"
	[#
	   var MyState     = false
	   var Countnb     = 0  //numero dei vicini
	   var Countnbon   = 0  //numero degli stati on dei vicini
	   var Countnbmsgs = 0  //numero dei msg ricevuti dai vicini	  
	   var  X          = 0
	   var  Y          = 0
	   var XD          = 0
	   var YD          = 0
	   
	   lateinit  var outindev  : OutInCellGui
	   val guiinterpreter = main.java.CellCmdTranslator( myself )  //ADDED
 		 
 fun setCellCoords( )  {
     val coords = name.replace("cell_","").split("_")  //Before cellc
     X = coords[0].toInt()
     XD = X+1
     Y  = coords[1].toInt()        
     YD =Y+1
  }
  
  fun displayOnGui(){ 
	val V   = if (MyState) 1 else 0
	val msg = "cell($XD,$YD,$V)" 
	outindev.display(msg)   	
  }
        
	#]
	State s0 initial{
		delay 3000 // connessione MQTT ....
		[# setCellCoords( ) #] 
		println("$name | START X=$X Y=$Y ") color green
		[#  //setCellCoords( )
			Countnb = conway.GridSupport25.subscribeToNeighborsMqtt(myself, X,Y) 
		#]       	
		println("$name | STARTS Countnb= $Countnb") color black
		
 		[# outindev = OutInCellGui(myself, "guiin") #]

    }  
	Transition t0  
 	    whenEvent stopthecell     -> stopthecell
		whenEvent clearCell       -> clearThecell
		whenMsg changeCellState   -> changeCellState
 		whenEvent startthegame    -> emitinfophase
		whenInterruptEvent kernel_rawmsg   -> handleGuiMsg

	State handleGuiMsg{
		printCurrentMessage color black
		onMsg( kernel_rawmsg : kernel_rawmsg(ARG)){
			[# guiinterpreter.cvtToApplMessage( payloadArg(0) ) #]
		}
		returnFromInterrupt
	}
	
	State changeCellState{
		onMsg( changeCellState : changeCellState(X,Y) ){
			if [# X == payloadArg(0).toInt() && Y == payloadArg(1).toInt() #]{ //RIGUARDA LA CELLA
				[#  MyState = ! MyState;
					displayOnGui()
				#]
				println("$name | changeCellState=$MyState ") color yellow
			 }
		}   
	}
	Transition t0  
 	    whenEvent stopthecell     -> stopthecell
  		whenEvent clearCell       -> clearThecell
		whenMsg changeCellState   -> changeCellState
		whenEvent startthegame    -> emitinfophase
		whenInterruptEvent kernel_rawmsg   -> handleGuiMsg
  	 
	State emitinfophase{
 		[# var MyInfo = "$name,$MyState" #]
 		println("$name | emitinfophase") color green

        [# emitstreammqtt(name, "curstate","curstate($MyInfo)") #]
         
         
 	}
	Transition t0   
	    whenEvent stopthecell  -> stopthecell
		whenEvent curstate    -> elabinfophase
		whenInterruptEvent kernel_rawmsg   -> handleGuiMsg
 				  
    State elabinfophase{
  		[# Countnbmsgs=Countnbmsgs+1 #]
 		println("$name | elabinfophase $Countnbmsgs") color blue
		onMsg ( curstate : curstate(NB,true) ){
			[# Countnbon = Countnbon + 1  #]
		} 
		if [# Countnbmsgs == Countnb #] {  		 
			autodispatch allnbreceived : allnbreceived(ok)
		}		  
    } 
	Transition t0 
	      whenEvent stopthecell   -> stopthecell
		  whenEvent curstate    -> elabinfophase
		  whenMsg allnbreceived -> elabstatephase
		  whenInterruptEvent kernel_rawmsg   -> handleGuiMsg
 	
	State elabstatephase{
 		println("$name | elabstatephase $MyState $Countnbon") color yellow
		if [# MyState  #]{ //soprevvice o muore
			[# MyState = Countnbon==2 || Countnbon==3 #]
		}
		else{ 
		  if [# ! MyState  #]{ //rinasce o rimane morta
			[# MyState = (Countnbon==3) #]
		  }
		}
		//Aggiorno il display 	 		
		[# Countnbmsgs = 0
		   Countnbon   = 0 
           displayOnGui() 
        #]  
        
        println("$name | elabstatephase emit event alarm JUST TO TEST") color magenta
        emit alarm : alarm($MyName)  //just to test ... Va a lifein Arriva anche a perceiveronpc
        println("$name | emitstreammqtt JUST TO TEST") color magenta
        [# emitstreammqtt("unibo/qak/cell_1_1", "alarm","alarm(${MyName}qak)") #]
  	}
	Transition t0 
		whenTime 1000           -> emitinfophase  //(no synch COREOGRAPHY)
 	    whenEvent stopthecell   -> stopthecell
        whenEvent clearCell     -> clearThecell 
      	whenMsg changeCellState -> changeCellState
		whenInterruptEvent kernel_rawmsg   -> handleGuiMsg
 
	State stopthecell{
		println("$name | stopthecell ") color magenta 
	}
    Transition t0  
    	whenMsg changeCellState -> changeCellState
        whenEvent startthegame  -> emitinfophase 
        whenEvent clearCell     -> clearThecell 
 	    whenInterruptEvent kernel_rawmsg -> handleGuiMsg
 
    State clearThecell{
    	//println("$name | clearThecell ")
     	[# MyState = false #]
     	[# displayOnGui() #] 
     } 
    Transition t0  
    	whenMsg changeCellState -> changeCellState
        whenEvent startthegame  -> emitinfophase 
        whenEvent clearCell     -> clearThecell 
  	    whenEvent stopthecell   -> stopthecell
        whenInterruptEvent kernel_rawmsg -> handleGuiMsg
 
 
 
}//cell 

/*
 * ------------------------------------------
 * perceiver
 * ------------------------------------------
 */
 
 QActor perceiveronpc context ctxcells{
 	State s0 initial{
 		println("$name | STARTS ") color blue
 		
 	}
 	Goto work
 	
 	State work{
 	}
 	Transition t0
 		whenEvent alarm -> handleEvent
 		
 	State handleEvent{
 		printCurrentMessage color blue
 	}
// 	Transition t0
// 		whenEvent alarm -> handleEvent
 	Goto work
 }