System conway25qak0

Dispatch startGame : startGame(ARG)    
Dispatch stopGame  : stopGame(ARG)
Dispatch clearGame : clearGame(ARG)
Dispatch exitGame  : exitGame(ARG) 
Dispatch cellstate : cellstate(X,Y)

Context ctxconway0 ip [host="localhost" port=8920 ]
 
QActor conway0 context ctxconway0 
	withobj life using "conwayMqtt.Life(20,20)"   //dalla library in applibs
	{ 
//import "conwayMqtt.*"
[#  
	//val life     = conwayMqtt.Life(20,20)
	val outdev   = main.java.conway.devices.OutInMqttForActor( myself  )	
	val helper   =  main.java.conway.LifeUsageHelper( life, outdev )
	//var running  = false
#]
	State s0 initial{
		println("$name | starts") color magenta
		[# 			 
			 helper.swithCellState(1,1)
 			 helper.swithCellState(1,2)
 			 helper.swithCellState(1,3)
		#]		
	}
	Goto work
	
	State work{}
	Transition t0 
  		whenMsg cellstate  -> changeCellState
  		whenMsg startGame  -> handleStartGame
		whenMsg stopGame   -> handleStopGame
		whenMsg clearGame  -> handleClearGame
		whenMsg exitGame   -> handleExit
	
	
	State changeCellState{
		printCurrentMessage
		onMsg( cellstate : cellstate(X,Y) ){
			[#
				val X = payloadArg(0).toInt()
				val Y = payloadArg(1).toInt()
				helper.swithCellState(X,Y)
			#]
		}
	}
	Goto work
	
	State handleStartGame{
		println("$name | starts the game") color magenta
		[# 
		   val goon = helper.fireEpoch()  //goon false se empty o stable
		#]
		if [# !goon #]{ autodispatch stopGame : stopGame(play) }
	}
	Transition t0 
  		whenTime 1000     -> handleStartGame
  		whenMsg stopGame  -> handleStopGame


	State handleStopGame{
		println("$name | stop the game") color magenta
		//[# running = false #] //Non serve, in quanto torna a work
	}
	Goto work

	State handleClearGame{
		println("$name | clear the game") color magenta
		[# helper.resetAndDisplayGrids() 
		#]
	}
	Goto work
	
	State handleExit{
		println("$name | EXIT from the game") color magenta
		[# System.exit(0) #]
	}
}

/*
 * Attore che 'ingloba' solo  Life  
 * e funge da controller che risponde a messaggi
 * inviati (anche) da OutInMqttForActor
 * La funzione displayCell invia
 * 
 */    