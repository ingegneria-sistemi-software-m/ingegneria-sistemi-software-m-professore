System unibomapbuild25//-msglog  
/*
========================================
mapobstaclesplan.qak in unibomapbuild25

Utilizza basicrobot  
========================================
*/
/*
 * docker cp sceneConfig.js 87c9eb2e0989:/home/node/WEnv/WebGLScene/sceneConfig.js
 */
Dispatch move      : cmd( MOVE )     

Dispatch cmd      : cmd( MOVE )   
Dispatch halt      : halt(X) 

Request step       : step( TIME )	
Reply stepdone     : stepdone(V)  for step
Reply stepfailed   : stepfailed(DURATION, CAUSE) for step

Event alarm        : alarm(X)
Event mapinfo      : mapinfo(X)

Request  doplan   : doplan( PATH, OWNER )
Reply doplandone  : doplandone( ARG )    for doplan
Reply doplanfail  : doplanfail( ARG )    for doplan

Request engage        : engage(OWNER, STEPTIME)	
Reply   engagedone    : engagedone(ARG)    for engage
Reply   engagerefused : engagerefused(ARG) for engage
Dispatch disengage    : disengage(ARG)

Context ctxmapbuild    ip [host="localhost"  port=8032  ]
Context ctxbasicrobot  ip [host="127.0.0.1"  port=8020  ]

ExternalQActor basicrobot context ctxbasicrobot
   
/*   
 * ------------------------------------------------------------------
 * Cerca di raggiungere una posizioe esterna alla mappa
 * Torna in HOME tutte le volte che trova un ostacolo
 * Termina quando il piano proposto ha lunghezza 0
 * ------------------------------------------------------------------
*/     
QActor mapbuilder  context ctxmapbuild{   //gui23xyz9526
	import  "unibo.planner23.*"
[#
 
val planner      = Planner23Util()
var CurPlan   = ""
var StepInPlan = false
var CurMove    = ""
var Athome     = false 
var PlanForHome=false
#]   
 
	State s0 initial {
		[#  planner.initAI() #]  
//		connectToMqttBroker "wss://test.mosquitto.org:8081"
//		subscribe "unibodisiplan"
 		forward basicrobot -m halt : halt(1)
 		delay 300
	} 
	//Goto explore
    Goto  engage  
	
	State engage{
		println("$name | $MyName engaging ... ") color yellow 
		request basicrobot -m engage : engage($MyName,350)
	}
	Transition t0 whenReply engagedone  ->  explore     //    explore test
 	              whenReply engagerefused -> endofwork
	
	State endofwork{ 
		println("$name | BYE ") color blue
	}
//-------------------------------------------------------------------------	
	State test{
		println("$name | testtttttttttttttttt ") color blue
		forward basicrobot -m cmd : cmd( h )
//   		forward basicrobot -m cmd : cmd(l) 
  		delay 1000
		println("$name | testtttttttttttttttt w ") color blue
    	forward basicrobot -m cmd : cmd( w ) //forever ...
		delay 350 
 		println("$name | testtttttttttttttttt h ") color blue
 		forward basicrobot -m cmd : cmd( h )
// 		delay 1000
//    		forward basicrobot -m cmd : cmd(r) 
		
		delay 1000
		[# System.exit(0) #]
//		request basicrobot -m step : step(350)
//		forward basicrobot -m disengage : disengage($MyName)
	}
	Transition t0 
		whenReply stepdone -> ok1
		whenReply stepfailed -> ko1
		
	State ok1{
		printCurrentMessage
		println("$name | ok1 disengage") color blue
		forward basicrobot -m disengage : disengage($MyName)
	}
	Goto endtest
	
	State ko1{
		printCurrentMessage
		println("$name | ko1 disengage") color blue
		forward basicrobot -m disengage : disengage($MyName)
	}
	Goto endtest
	
	State endtest{
		[# System.exit(0) #]
	}
//-------------------------------------------------------------------------	
	
	State explore{
 		[# planner.setGoal(7,6); PlanForHome = false #] 		
 		[# CurPlan = planner.doPlanCompact() #]		
 		[# planner.showMap() #]
 		println("$CurPlan") color blue
	}
	Goto execThePlan if [# CurPlan.length>0 #] else workdone
	
	State workdone{
		println("$name | workdone ") color blue
		[# planner.showCurrentRobotState() #]
		[# planner.saveRoomMap("map2024ok")  #]
	}

	State execThePlan{
		println("$name | execThePlan CurPlan=$CurPlan") color cyan
		if [# CurPlan.length > 0 #]{
			[#  CurMove = ""+CurPlan[0]; 
				CurPlan = CurPlan.drop(1) 
			#]
		    //println("$name | execThePlan CurMove=$CurMove") color blue		
		}else{ [# CurMove="" #] }
	}
	Goto doMove if [# (CurMove.length > 0) #] else endOfPlan
	
	State doMove{
		println("$name | doMove  $CurMove") color cyan
		delay 100
		if [# CurMove == "w" #]{ 
 			[# StepInPlan = true  #]
        }   
		if [# CurMove == "l" #]{ 
			forward basicrobot -m cmd : cmd( l )
			[#  planner.updateMap(  "l", "" )  #] 
			[# StepInPlan = false #]
        }
		if [# CurMove == "r" #]{ 
			forward basicrobot -m cmd : cmd( r )
			[#  planner.updateMap(  "r", "" )  #] 
			[# StepInPlan = false  #]
        }         
        [# val MAP = planner.getMapOneLine() #]
//        publish "unibodisiplan" -m mapinfo : mapinfo($MAP)
	}
	Goto dostep if [# StepInPlan #] else execThePlan 
 
    State dostep{ 
    	println("$name | doStep  $CurMove") color blue
 		request basicrobot -m step : step(350)    
    }
	Transition t0  
 		whenReply stepdone   -> stepcompleted   
	    whenReply stepfailed -> planko

	State stepcompleted{ //Per aggiornare la mappa
	    //Sarebbe bene che fosse basicrobot aad ggiornasse la mappa ...
	    //println("$name | stepcompleted ") color blue
 		[# planner.updateMap(  "w", "" ) #]  	
	}
	Goto execThePlan
	
	State endOfPlan{
 		[# planner.showCurrentRobotState() #]
		[# Athome = planner.atHome() #]
		println("$name | endOfPlan with Athome=$Athome ") color magenta
	}
	Goto explore if [# !Athome #] else tuneAtHome
 
    State tuneAtHome{
    	delay 500
    	[# Athome  = false #]
    	[# val Dir = planner.getDirection() #]
    	println("$name | tuneAtHome Dir=$Dir") color magenta
    	if [# Dir == "upDir" #]{
 
	    	forward basicrobot -m cmd : cmd( w ) //forever ...
			delay 350 
	 		println("$name | upDir tune h ---------------- ") color blue
	 		forward basicrobot -m cmd : cmd( h )

            forward basicrobot -m cmd : cmd( r )
            forward basicrobot -m cmd : cmd( w )
			delay 350 
	 		println("$name | upDir tune h ---------------- ") color blue
            forward basicrobot -m cmd : cmd( h )
            forward basicrobot -m cmd : cmd( l )
            
//    		forward basicrobot -m move : cmd( w )
//    		forward basicrobot -m cmd : cmd( l )
//    		forward basicrobot -m move : cmd( w )
//    		forward basicrobot -m cmd : cmd( r )
//    		forward basicrobot -m move : cmd( w )
    	}
    	if [# Dir == "leftDir" #]{
	    	forward basicrobot -m cmd : cmd( w ) //forever ...
			delay 350 
	 		println("$name | leftDir tune h -------------- ") color blue
	 		forward basicrobot -m cmd : cmd( h )
            forward basicrobot -m move : cmd( l )
     		forward basicrobot -m move : cmd( w )
			delay 350 
	 		println("$name | leftDir tune h -------------- ") color blue
            forward basicrobot -m move : cmd( r )
     		forward basicrobot -m move : cmd( w )
			delay 350 
	 		forward basicrobot -m cmd : cmd( h )
//    		forward basicrobot -m move : cmd( r )
//    		forward basicrobot -m move : cmd( w )
//    		forward basicrobot -m move : cmd( l )
    	}
    	[# planner.showCurrentRobotState() #]
		//[# CommUtils.waitTheUser("hit to explore"); #]
    	delay 1500
     }
     Goto explore
	
	State planko{
		println("$name | planko with CurPlan=$CurPlan PlanForHome=$PlanForHome") color magenta
		
		if [# PlanForHome #] {
			[# planner.updateMap(  "w", "" ) #]      //ignoro la collision
			println("$name | Ignore collision while goingback to HOME") color red
//			[# planner.saveRoomMap("map2024fatal")  #]
		    [# planner.showCurrentRobotState() #]
			delay 3000
//			[# System.exit(0) #]	
		}
		else{
			[# planner.updateMapObstacleOnCurrentDirection() #]  
		}	
		 
	}
	Goto execThePlan if [# PlanForHome #] else backToHome  //ignoro una collision mentre  backToHome

	State backToHome{  		
		println("$name | ++++ going backToHome ++++ ")   color magenta
   		[#   
   			planner.setGoal(0,0)
   			PlanForHome = true
   			CurPlan = planner.doPlanCompact()  			
   		#]
   		 
	}	
	Goto execThePlan
	
}//mapbuilder


/*
 *  
=================================================== |
Planner23Util doPlan NO MOVES !!! []
mapbuilder | workdone
| ===================================================
|r, 1, 1, 1, 1, 1, 1, X,
|1, 1, 1, 1, X, X, 1, X,
|1, 1, 1, 1, X, 0, 1, X,
|1, 1, X, 1, 1, 1, 1, X,
|1, 1, 1, 1, 1, 1, 1, X,
|X, X, X, X, X, X, X, 0, 

RobotPos=(0,0) direction=LEFT
 =================================================== |
saveMap in map2024ok 
*/